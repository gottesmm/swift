// RUN: %target-sil-opt -enable-sil-ownership -enable-sil-verify-all=0 -module-name Swift -o /dev/null %s 2>&1
// REQUIRES: asserts

// Make sure that we properly handle unreachable code the likes of which come
// from SILGen. This file should never crash.

sil_stage raw

import Builtin

//////////////////
// Declarations //
//////////////////

enum Never {}

sil @no_return_function : $@convention(thin) () -> Never
sil @create_object : $@convention(thin) () -> @owned Builtin.NativeObject
sil @use_object : $@convention(thin) (@owned Builtin.NativeObject) -> ()

///////////
// Tests //
///////////

sil @test1 : $@convention(thin) () -> () {
bb0:
  cond_br undef, bb1, bb12

bb1:
  %0 = alloc_box ${ var Builtin.Int32 }
  cond_br undef, bb3, bb2

bb2:
  dealloc_box %0 : ${ var Builtin.Int32 }
  br bb10

bb3:
  cond_br undef, bb4, bb5

bb4:
  br bb9

bb5:
  br bb6

bb6:
  cond_br undef, bb7, bb8

bb7:
  br bb6

bb8:
  br bb9

bb9:
  destroy_value %0 : ${ var Builtin.Int32 }
  br bb13

bb10:
  %1 = function_ref @no_return_function : $@convention(thin) () -> Never
  apply %1() : $@convention(thin) () -> Never
  br bb11

bb11:
  unreachable

bb12:
  br bb13

bb13:
  %9999 = tuple()
  return %9999 : $()
}

sil @ignore_if_earlier_in_block : $@convention(thin) () -> () {
bb0:
  %0 = function_ref @create_object : $@convention(thin) () -> @owned Builtin.NativeObject
  %1 = function_ref @no_return_function : $@convention(thin) () -> Never

  %2 = apply %0() : $@convention(thin) () -> @owned Builtin.NativeObject
  destroy_value %2 : $Builtin.NativeObject
  apply %1() : $@convention(thin) () -> Never
  br bb1

bb1:
  %9999 = tuple()
  return %9999 : $()
}

// This test makes sure that if we have multiple unreachable blocks and control
// flow after-wards, that we properly ignore later users.
sil @dont_ignore_if_earlier_in_block_but_already_dead : $@convention(thin) () -> () {
bb0:
  %0 = function_ref @create_object : $@convention(thin) () -> @owned Builtin.NativeObject
  %1 = function_ref @no_return_function : $@convention(thin) () -> Never
  %2 = apply %0() : $@convention(thin) () -> @owned Builtin.NativeObject
  destroy_value %2 : $Builtin.NativeObject
  apply %1() : $@convention(thin) () -> Never
  br bb1

bb1:
  destroy_value %2 : $Builtin.NativeObject
  apply %1() : $@convention(thin) () -> Never
  br bb2

bb2:
  destroy_value %2 : $Builtin.NativeObject
  %9999 = tuple()
  return %9999 : $()
}

sil @builtins_1 : $@convention(thin) () -> () {
bb0:
  %0 = alloc_box ${ var Builtin.Int32 }
  destroy_value %0 : ${ var Builtin.Int32 }
  %1 = builtin "unreachable"() : $Never
  br bb1

bb1:
  destroy_value %0 : ${ var Builtin.Int32 }
  %9999 = tuple()
  return %9999 : $()
}
